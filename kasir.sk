var DB_URL = "jdbc:postgresql://localhost:5432/kasir_db"
var DB_USER = "postgres"
var DB_PASSWORD = "admin"

var userLogin = null

var buatKoneksi = fn() {
    var conn = database("org.postgresql.Driver", DB_URL, DB_USER, DB_PASSWORD)
    return conn
}

var eksekusiQuery = fn(q, params) {
    var conn = buatKoneksi()
    if (conn == null) {
        message("Koneksi database gagal!")
        return null
    }
    var hasil = query(conn, [[q, params]])
    return hasil
}

var hashPassword = fn(password) {
    var h = 0
    var i = 0
    repeat {
        if (i >= len(password)) {
            return string(h)
        }
        var char = password[i]
        var h = ((h * 31) + ord(char))
        var i = i + 1
    }
}

var daftar = fn() {
    var username = input("Username:", "Pendaftaran User Baru")
    if (empty(username)) {
        return false
    }
    
    var pwd = password("Password:", "Pendaftaran User Baru")
    if (empty(pwd)) {
        return false
    }
    
    var namaLengkap = input("Nama Lengkap:", "Pendaftaran User Baru")
    if (empty(namaLengkap)) {
        return false
    }
    
    var hashedPassword = hashPassword(pwd)
    var q = "INSERT INTO users (username, password, nama_lengkap) VALUES (?, ?, ?)"
    var hasil = eksekusiQuery(q, [username, hashedPassword, namaLengkap])
    
    if (hasil != null) {
        message("Pendaftaran berhasil! Silakan login.")
        return true
    }
    message("Error: Username sudah digunakan")
    return false
}

var login = fn() {
    var loginData = login_dialog("Login")
    
    if (len(loginData) != 2) {
        return false
    }
    
    var username = loginData[0]
    var pwd = loginData[1]
    
    var hashedPassword = hashPassword(pwd)
    var q = "SELECT id, username, nama_lengkap FROM users WHERE username = ? AND password = ?"
    var hasil = eksekusiQuery(q, [username, hashedPassword])
    
    if (hasil != null) {
        if (len(hasil) > 0) {
            if (len(hasil[0]) > 0) {
                var userLogin = hasil[0][0]
                message("Login berhasil! Selamat datang, " + userLogin[2])
                return true
            }
        }
    }
    
    message("Username atau password salah!")
    return false
}

var logout = fn() {
    var userLogin = null
    message("Logout berhasil!")
}

var cekLogin = fn() {
    return userLogin != null
}

var dapatkanUserId = fn() {
    if (userLogin != null) {
        return userLogin[0]
    }
    return null
}

var lihatSemuaProduk = fn() {
    var q = "SELECT id, nama, harga, stok, kategori FROM products ORDER BY kategori, nama"
    var hasil = eksekusiQuery(q, [])
    if (hasil != null) {
        if (len(hasil) > 0) {
            return hasil[0]
        }
    }
    return []
}

var cariProduk = fn() {
    var keyword = input("Masukkan kata kunci pencarian:", "Cari Produk")
    if (empty(keyword)) {
        return null
    }
    
    var q = "SELECT id, nama, harga, stok, kategori FROM products WHERE nama ILIKE ? ORDER BY nama"
    var hasil = eksekusiQuery(q, ["%" + keyword + "%"])
    
    if (hasil == null) {
        message("Produk tidak ditemukan")
        return null
    }
    
    if (len(hasil) == 0) {
        message("Produk tidak ditemukan")
        return null
    }
    
    if (len(hasil[0]) == 0) {
        message("Produk tidak ditemukan")
        return null
    }
    
    reset()
    var tabel = component("table", "ID,Nama,Harga,Stok,Kategori", true)
    config(tabel, "contents", hasil[0])
    
    var btnKembali = component("button", "Kembali ke Daftar Produk")
    
    event(btnKembali, fn() {
        frame_close()
        tampilkanProduk()
    })
    
    add(tabel)
    add_s(btnKembali)
    title("Hasil Pencarian: " + keyword)
    show()
}

var tampilkanProduk = fn() {
    var products = lihatSemuaProduk()
    
    if (len(products) == 0) {
        message("Tidak ada produk tersedia")
        return null
    }
    
    reset()
    var tabel = component("table", "ID,Nama,Harga,Stok,Kategori", true)
    config(tabel, "contents", products)
    
    var btnCari = component("button", "Cari Produk")
    var btnTambahKeranjang = component("button", "Tambah ke Keranjang")
    var btnKeranjang = component("button", "Lihat Keranjang")
    var btnRiwayat = component("button", "Riwayat Transaksi")
    var btnMenu = component("button", "Menu Utama")
    var btnTutup = component("button", "Tutup")
    
    event(btnCari, fn() {
        cariProduk()
    })
    
    event(btnTambahKeranjang, fn() {
        tambahKeKeranjang()
    })
    
    event(btnKeranjang, fn() {
        tampilkanKeranjang()
    })
    
    event(btnRiwayat, fn() {
        lihatRiwayatTransaksi()
    })
    
    event(btnMenu, fn() {
        frame_close()
        tampilkanMenuUtama()
    })
    
    event(btnTutup, fn() {
        frame_close()
    })
    
    add(tabel)
    add_s([btnCari, btnTambahKeranjang, btnKeranjang, btnRiwayat, btnMenu, btnTutup])
    title("Daftar Produk")
    show()
}

var tambahProduk = fn() {
    var nama = input("Nama Produk:", "Tambah Produk Baru")
    if (empty(nama)) {
        return false
    }
    
    var hargaStr = input("Harga:", "Tambah Produk Baru")
    if (empty(hargaStr)) {
        return false
    }
    var harga = number(hargaStr)
    
    var stokStr = input("Stok:", "Tambah Produk Baru")
    if (empty(stokStr)) {
        return false
    }
    var stok = number(stokStr)
    
    var kategori = input("Kategori (Makanan/Minuman/Lainnya):", "Tambah Produk Baru")
    if (empty(kategori)) {
        return false
    }
    
    var q = "INSERT INTO products (nama, harga, stok, kategori) VALUES (?, ?, ?, ?)"
    eksekusiQuery(q, [nama, harga, stok, kategori])
    
    message("Produk berhasil ditambahkan!")
    return true
}

var editProduk = fn() {
    var idStr = input("Masukkan ID Produk yang akan diedit:", "Edit Produk")
    if (empty(idStr)) {
        return false
    }
    var productId = number(idStr)
    
    var qCek = "SELECT nama, harga, stok, kategori FROM products WHERE id = ?"
    var hasilCek = eksekusiQuery(qCek, [productId])
    
    if (hasilCek == null) {
        message("Produk tidak ditemukan!")
        return false
    }
    
    if (len(hasilCek) == 0) {
        message("Produk tidak ditemukan!")
        return false
    }
    
    if (len(hasilCek[0]) == 0) {
        message("Produk tidak ditemukan!")
        return false
    }
    
    var produkLama = hasilCek[0][0]
    
    var nama = input("Nama Produk [" + produkLama[0] + "]:", "Edit Produk")
    if (empty(nama)) {
        var nama = produkLama[0]
    }
    
    var hargaStr = input("Harga [" + produkLama[1] + "]:", "Edit Produk")
    var harga = produkLama[1]
    if (empty(hargaStr) == false) {
        var harga = number(hargaStr)
    }
    
    var stokStr = input("Stok [" + produkLama[2] + "]:", "Edit Produk")
    var stok = produkLama[2]
    if (empty(stokStr) == false) {
        var stok = number(stokStr)
    }
    
    var kategori = input("Kategori [" + produkLama[3] + "]:", "Edit Produk")
    if (empty(kategori)) {
        var kategori = produkLama[3]
    }
    
    var q = "UPDATE products SET nama = ?, harga = ?, stok = ?, kategori = ? WHERE id = ?"
    eksekusiQuery(q, [nama, harga, stok, kategori, productId])
    
    message("Produk berhasil diupdate!")
    return true
}

var hapusProduk = fn() {
    var idStr = input("Masukkan ID Produk yang akan dihapus:", "Hapus Produk")
    if (empty(idStr)) {
        return false
    }
    var productId = number(idStr)
    
    var konfirmasi = confirm("Yakin ingin menghapus produk ini?", "Konfirmasi")
    if (konfirmasi == "OK") {
        var q = "DELETE FROM products WHERE id = ?"
        eksekusiQuery(q, [productId])
        message("Produk berhasil dihapus!")
        return true
    }
    return false
}

var lihatKeranjang = fn() {
    var userId = dapatkanUserId()
    if (userId == null) {
        return []
    }
    var q = "SELECT k.id, p.nama, p.harga, k.jumlah, (p.harga * k.jumlah) as subtotal, k.product_id FROM keranjang k JOIN products p ON k.product_id = p.id WHERE k.user_id = ?"
    var hasil = eksekusiQuery(q, [userId])
    if (hasil != null) {
        if (len(hasil) > 0) {
            return hasil[0]
        }
    }
    return []
}

var tampilkanKeranjang = fn() {
    var items = lihatKeranjang()
    
    if (len(items) == 0) {
        message("Keranjang kosong")
        return null
    }
    
    reset()
    var tabel = component("table", "ID,Produk,Harga,Qty,Subtotal", true)
    
    var dataKeranjang = []
    var total = 0
    
    each(items, fn(item, i) {
        var dataKeranjang = dataKeranjang + [[item[0], item[1], item[2], item[3], item[4]]]
        var total = total + item[4]
    })
    
    config(tabel, "contents", dataKeranjang)
    
    var lblTotal = component("label", "Total: Rp " + total)
    config(lblTotal, "font", ["monospaced", 1, 18])
    
    var btnCheckout = component("button", "Checkout")
    var btnHapus = component("button", "Hapus Item")
    var btnKosongkan = component("button", "Kosongkan Keranjang")
    var btnTutup = component("button", "Tutup")
    
    event(btnCheckout, fn() {
        var metodePembayaran = input("Metode Pembayaran (Tunai/Kartu/Transfer):", "Checkout")
        if (empty(metodePembayaran)) {
            var metodePembayaran = "Tunai"
        }
        
        var konfirmasi = confirm("Lanjutkan checkout dengan " + metodePembayaran + "?", "Konfirmasi")
        if (konfirmasi == "OK") {
            checkoutDenganMetode(metodePembayaran)
            frame_close()
        }
    })
    
    event(btnHapus, fn() {
        var idStr = input("Masukkan ID item yang akan dihapus:", "Hapus Item")
        if (empty(idStr) == false) {
            var itemId = number(idStr)
            var userId = dapatkanUserId()
            var q = "DELETE FROM keranjang WHERE id = ? AND user_id = ?"
            eksekusiQuery(q, [itemId, userId])
            message("Item berhasil dihapus!")
            frame_close()
            tampilkanKeranjang()
        }
    })
    
    event(btnKosongkan, fn() {
        var konfirmasi = confirm("Kosongkan semua item di keranjang?", "Konfirmasi")
        if (konfirmasi == "OK") {
            var userId = dapatkanUserId()
            var q = "DELETE FROM keranjang WHERE user_id = ?"
            eksekusiQuery(q, [userId])
            message("Keranjang berhasil dikosongkan!")
            frame_close()
        }
    })
    
    event(btnTutup, fn() {
        frame_close()
    })
    
    add(tabel)
    add_s([lblTotal, btnCheckout, btnHapus, btnKosongkan, btnTutup])
    title("Keranjang Belanja")
    show()
}

var tambahKeKeranjang2 = fn(productId, jumlah) {
    var userId = dapatkanUserId()
    
    if (userId == null) {
        message("Anda harus login terlebih dahulu!")
        return false
    }
    
    if (jumlah <= 0) {
        message("Jumlah harus lebih dari 0!")
        return false
    }
    
    var qStok = "SELECT stok, nama FROM products WHERE id = ?"
    var hasilStok = eksekusiQuery(qStok, [productId])
    
    if (hasilStok == null) {
        message("Produk tidak ditemukan!")
        return false
    }
    
    if (len(hasilStok) == 0) {
        message("Produk tidak ditemukan!")
        return false
    }
    
    if (len(hasilStok[0]) == 0) {
        message("Produk tidak ditemukan!")
        return false
    }
    
    var stokTersedia = hasilStok[0][0][0]
    var namaProduk = hasilStok[0][0][1]
    
    if (stokTersedia < jumlah) {
        message("Stok tidak mencukupi!" + newline() + "Stok tersedia: " + stokTersedia)
        return false
    }
    
    var qCheck = "SELECT id, jumlah FROM keranjang WHERE user_id = ? AND product_id = ?"
    var hasilCheck = eksekusiQuery(qCheck, [userId, productId])
    
    if (hasilCheck != null) {
        if (len(hasilCheck) > 0) {
            if (len(hasilCheck[0]) > 0) {
                var jumlahLama = hasilCheck[0][0][1]
                var jumlahBaru = jumlahLama + jumlah
                
                if (stokTersedia < jumlahBaru) {
                    message("Total jumlah melebihi stok!" + newline() + "Di keranjang: " + jumlahLama + newline() + "Stok tersedia: " + stokTersedia)
                    return false
                }
                
                var qUpdate = "UPDATE keranjang SET jumlah = ? WHERE user_id = ? AND product_id = ?"
                var hasilUpdate = eksekusiQuery(qUpdate, [jumlahBaru, userId, productId])
                
                if (hasilUpdate != null) {
                    message(namaProduk + " berhasil ditambahkan ke keranjang!" + newline() + "Total di keranjang: " + jumlahBaru)
                    return true
                }
            }
        }
    }
    
    var q = "INSERT INTO keranjang (user_id, product_id, jumlah) VALUES (?, ?, ?)"
    var hasilInsert = eksekusiQuery(q, [userId, productId, jumlah])
    
    if (hasilInsert != null) {
        message(namaProduk + " berhasil ditambahkan ke keranjang!")
        return true
    }
    
    message("Gagal menambahkan produk ke keranjang!")
    return false
}

var tambahKeKeranjang = fn() {
    var userId = dapatkanUserId()
    
    if (userId == null) {
        message("Anda harus login terlebih dahulu!")
        return false
    }
    
    var productIdStr = input("Masukkan ID Produk:", "Tambah ke Keranjang")
    if (empty(productIdStr)) {
        return false
    }
    var productId = number(productIdStr)
    
    var jumlahStr = input("Jumlah:", "Tambah ke Keranjang")
    if (empty(jumlahStr)) {
        return false
    }
    var jumlah = number(jumlahStr)
    
    return tambahKeKeranjang2(productId, jumlah)
}

var checkoutDenganMetode = fn(metodePembayaran) {
    var userId = dapatkanUserId()
    
    if (userId == null) {
        message("Anda harus login terlebih dahulu!")
        return false
    }
    
    var items = lihatKeranjang()
    
    if (len(items) == 0) {
        message("Keranjang kosong!")
        return false
    }
    
    var total = 0
    each(items, fn(item, i) {
        var total = total + item[4]
    })
    
    var qTransaksi = "INSERT INTO transaksi (user_id, total, metode_pembayaran, status) VALUES (?, ?, ?, ?)"
    var hasilTransaksi = eksekusiQuery(qTransaksi, [userId, total, metodePembayaran, "selesai"])
    
    if (hasilTransaksi == null) {
        message("Terjadi kesalahan saat checkout")
        return false
    }
    
    var qId = "SELECT currval('transaksi_id_seq')"
    var transaksiId = eksekusiQuery(qId, [])
    
    if (transaksiId != null) {
        if (len(transaksiId) > 0) {
            if (len(transaksiId[0]) > 0) {
                var tid = transaksiId[0][0][0]
                
                each(items, fn(item, i) {
                    var qDetail = "INSERT INTO transaksi_detail (transaksi_id, product_id, jumlah, harga_satuan, subtotal) VALUES (?, ?, ?, ?, ?)"
                    eksekusiQuery(qDetail, [tid, item[5], item[3], item[2], item[4]])
                    
                    var qUpdateStok = "UPDATE products SET stok = stok - ? WHERE id = ?"
                    eksekusiQuery(qUpdateStok, [item[3], item[5]])
                })
                
                var qKosongkan = "DELETE FROM keranjang WHERE user_id = ?"
                eksekusiQuery(qKosongkan, [userId])
                
                message("Transaksi berhasil!" + newline() + "ID Transaksi: #" + tid + newline() + "Total: Rp " + total + newline() + "Metode: " + metodePembayaran)
                return true
            }
        }
    }
    
    message("Terjadi kesalahan saat checkout")
    return false
}

var lihatRiwayatTransaksi = fn() {
    var userId = dapatkanUserId()
    var q = "SELECT t.id, t.total, t.tanggal, t.metode_pembayaran FROM transaksi t WHERE t.user_id = ? ORDER BY t.tanggal DESC LIMIT 20"
    var hasil = eksekusiQuery(q, [userId])
    
    if (hasil == null) {
        message("Belum ada transaksi")
        return null
    }
    
    if (len(hasil) == 0) {
        message("Belum ada transaksi")
        return null
    }
    
    if (len(hasil[0]) == 0) {
        message("Belum ada transaksi")
        return null
    }
    
    reset()
    var tabel = component("table", "ID,Total,Tanggal,Metode", true)
    config(tabel, "contents", hasil[0])
    
    var btnLihatDetail = component("button", "Lihat Detail")
    var btnCetakStruk = component("button", "Cetak Struk")
    var btnTutup = component("button", "Tutup")
    
    event(btnLihatDetail, fn() {
        var idStr = input("Masukkan ID Transaksi:", "Lihat Detail")
        if (empty(idStr) == false) {
            var transaksiId = number(idStr)
            lihatDetailTransaksi(transaksiId)
        }
    })
    
    event(btnCetakStruk, fn() {
        var idStr = input("Masukkan ID Transaksi:", "Cetak Struk")
        if (empty(idStr) == false) {
            var transaksiId = number(idStr)
            cetakStruk(transaksiId)
        }
    })
    
    event(btnTutup, fn() {
        frame_close()
    })
    
    add(tabel)
    add_s([btnLihatDetail, btnCetakStruk, btnTutup])
    title("Riwayat Transaksi")
    show()
}

var lihatDetailTransaksi = fn(transaksiId) {
    var q = "SELECT p.nama, td.jumlah, td.harga_satuan, td.subtotal FROM transaksi_detail td JOIN products p ON td.product_id = p.id WHERE td.transaksi_id = ?"
    var detail = eksekusiQuery(q, [transaksiId])
    
    if (detail == null) {
        message("Detail transaksi tidak ditemukan")
        return null
    }
    
    if (len(detail) == 0) {
        message("Detail transaksi tidak ditemukan")
        return null
    }
    
    if (len(detail[0]) == 0) {
        message("Detail transaksi tidak ditemukan")
        return null
    }
    
    reset()
    var tabel = component("table", "Produk,Qty,Harga,Subtotal", true)
    config(tabel, "contents", detail[0])
    
    var total = 0
    each(detail[0], fn(item, i) {
        var total = total + item[3]
    })
    
    var lblTotal = component("label", "Total: Rp " + total)
    config(lblTotal, "font", ["monospaced", 1, 18])
    
    var btnTutup = component("button", "Tutup")
    
    event(btnTutup, fn() {
        frame_close()
    })
    
    add(tabel)
    add_s([lblTotal, btnTutup])
    title("Detail Transaksi #" + transaksiId)
    show()
}

var cetakStruk = fn(transaksiId) {
    var qHeader = "SELECT t.id, t.total, t.tanggal, t.metode_pembayaran, u.nama_lengkap FROM transaksi t JOIN users u ON t.user_id = u.id WHERE t.id = ?"
    var header = eksekusiQuery(qHeader, [transaksiId])
    
    if (header == null) {
        message("Transaksi tidak ditemukan")
        return null
    }
    
    var qDetail = "SELECT p.nama, td.jumlah, td.harga_satuan, td.subtotal FROM transaksi_detail td JOIN products p ON td.product_id = p.id WHERE td.transaksi_id = ?"
    var detail = eksekusiQuery(qDetail, [transaksiId])
    
    if (detail == null) {
        message("Detail transaksi tidak ditemukan")
        return null
    }
    
    var struk = "========================================" + newline()
    var struk = struk + "        APLIKASI KASIR" + newline()
    var struk = struk + "========================================" + newline()
    var struk = struk + "ID Transaksi: #" + transaksiId + newline()
    var struk = struk + "Tanggal: " + header[0][0][2] + newline()
    var struk = struk + "Kasir: " + header[0][0][4] + newline()
    var struk = struk + "Metode: " + header[0][0][3] + newline()
    var struk = struk + "========================================" + newline()
    var struk = struk + "Produk             Qty  Harga  Subtotal" + newline()
    var struk = struk + "----------------------------------------" + newline()
    
    each(detail[0], fn(item, i) {
        var struk = struk + item[0] + " " + item[1] + " " + item[2] + " " + item[3] + newline()
    })
    
    var struk = struk + "========================================" + newline()
    var struk = struk + "TOTAL: Rp " + header[0][0][1] + newline()
    var struk = struk + "========================================" + newline()
    var struk = struk + "    Terima kasih atas kunjungan Anda" + newline()
    
    reset()
    var textStruk = component("text", struk)
    config(textStruk, "font", ["monospaced", 0, 12])
    
    var btnCetak = component("button", "Print")
    var btnTutup = component("button", "Tutup")
    
    event(btnCetak, fn() {
        printer([struk], 12, 50, 50, "monospaced")
    })
    
    event(btnTutup, fn() {
        frame_close()
    })
    
    add(textStruk)
    add_s([btnCetak, btnTutup])
    title("Struk Transaksi #" + transaksiId)
    show()
}

var laporanHarian = fn() {
    var tanggalStr = input("Tanggal (YYYY-MM-DD), kosongkan untuk hari ini:", "Laporan Harian")
    var tanggal = "CURRENT_DATE"
    if (empty(tanggalStr) == false) {
        var tanggal = "'" + tanggalStr + "'"
    }
    
    var q = "SELECT COUNT(*) as total_transaksi, COALESCE(SUM(total), 0) as total_pendapatan FROM transaksi WHERE DATE(tanggal) = " + tanggal
    var hasil = eksekusiQuery(q, [])
    
    if (hasil == null) {
        message("Tidak ada data")
        return null
    }
    
    var totalTransaksi = hasil[0][0][0]
    var totalPendapatan = hasil[0][0][1]
    
    var qProduk = "SELECT p.nama, SUM(td.jumlah) as terjual FROM transaksi_detail td JOIN products p ON td.product_id = p.id JOIN transaksi t ON td.transaksi_id = t.id WHERE DATE(t.tanggal) = " + tanggal + " GROUP BY p.nama ORDER BY terjual DESC LIMIT 10"
    var produkTerlaris = eksekusiQuery(qProduk, [])
    
    reset()
    
    var lblJudul = component("label", "LAPORAN HARIAN")
    config(lblJudul, "font", ["monospaced", 1, 20])
    
    var lblTotal = component("label", "Total Transaksi: " + totalTransaksi)
    var lblPendapatan = component("label", "Total Pendapatan: Rp " + totalPendapatan)
    config(lblPendapatan, "font", ["monospaced", 1, 16])
    
    var tabel = component("table", "Produk,Terjual", true)
    if (produkTerlaris != null) {
        if (len(produkTerlaris) > 0) {
            config(tabel, "contents", produkTerlaris[0])
        }
    }
    
    var btnTutup = component("button", "Tutup")
    
    event(btnTutup, fn() {
        frame_close()
    })
    
    add_n([lblJudul, lblTotal, lblPendapatan])
    add(tabel)
    add_s(btnTutup)
    title("Laporan Harian")
    show()
}

var keluarAplikasi = fn() {
    var konfirmasi = confirm("Yakin ingin keluar dari aplikasi?", "Konfirmasi Keluar")
    if (konfirmasi == "OK") {
        message("Terima kasih telah menggunakan Aplikasi Kasir!")
        system(["exit", "0"])
    }
}

var tampilkanMenuUtama = fn() {
    reset()
    
    var lblJudul = component("label", "APLIKASI KASIR")
    config(lblJudul, "font", ["monospaced", 1, 24])
    
    var grid = component("grid", "")
    
    if (cekLogin()) {
        var lblUser = component("label", "User: " + userLogin[2])
        config(lblUser, "font", ["monospaced", 0, 14])
        
        var btnProduk = component("button", "Lihat Produk")
        var btnTambahKeranjang = component("button", "Tambah ke Keranjang")
        var btnKeranjang = component("button", "Lihat Keranjang")
        var btnRiwayat = component("button", "Riwayat Transaksi")
        var btnLaporan = component("button", "Laporan Harian")
        var btnTambahProduk = component("button", "Tambah Produk")
        var btnEditProduk = component("button", "Edit Produk")
        var btnHapusProduk = component("button", "Hapus Produk")
        var btnLogout = component("button", "Logout")
        var btnKeluar = component("button", "Keluar")
        
        grid_add(grid, lblUser, 0, 0, 1, 1, 1, 0, 3, 1, 5, 5, 5, 5)
        grid_add(grid, btnProduk, 0, 1, 1, 1, 1, 0, 3, 1, 5, 5, 5, 5)
        grid_add(grid, btnTambahKeranjang, 0, 2, 1, 1, 1, 0, 3, 1, 5, 5, 5, 5)
        grid_add(grid, btnKeranjang, 0, 3, 1, 1, 1, 0, 3, 1, 5, 5, 5, 5)
        grid_add(grid, btnRiwayat, 0, 4, 1, 1, 1, 0, 3, 1, 5, 5, 5, 5)
        grid_add(grid, btnLaporan, 0, 5, 1, 1, 1, 0, 3, 1, 5, 5, 5, 5)
        grid_add(grid, btnTambahProduk, 0, 6, 1, 1, 1, 0, 3, 1, 5, 5, 5, 5)
        grid_add(grid, btnEditProduk, 0, 7, 1, 1, 1, 0, 3, 1, 5, 5, 5, 5)
        grid_add(grid, btnHapusProduk, 0, 8, 1, 1, 1, 0, 3, 1, 5, 5, 5, 5)
        grid_add(grid, btnLogout, 0, 9, 1, 1, 1, 0, 3, 1, 5, 5, 5, 5)
        grid_add(grid, btnKeluar, 0, 10, 1, 1, 1, 0, 3, 1, 5, 5, 5, 5)
        
        event(btnProduk, fn() { tampilkanProduk() })
        event(btnTambahKeranjang, fn() { tambahKeKeranjang() })
        event(btnKeranjang, fn() { tampilkanKeranjang() })
        event(btnRiwayat, fn() { lihatRiwayatTransaksi() })
        event(btnLaporan, fn() { laporanHarian() })
        event(btnTambahProduk, fn() { tambahProduk() })
        event(btnEditProduk, fn() { editProduk() })
        event(btnHapusProduk, fn() { hapusProduk() })
        event(btnLogout, fn() { logout(); tampilkanMenuUtama() })
        event(btnKeluar, fn() { keluarAplikasi() })
    } else {
        var btnLogin = component("button", "Login")
        var btnDaftar = component("button", "Daftar")
        
        grid_add(grid, btnLogin, 0, 0, 1, 1, 1, 0, 3, 1, 5, 5, 5, 5)
        grid_add(grid, btnDaftar, 0, 1, 1, 1, 1, 0, 3, 1, 5, 5, 5, 5)
        
        event(btnLogin, fn() { 
            if (login()) { 
                frame_close()
                tampilkanProduk()
            } 
        })
        event(btnDaftar, fn() { 
            if (daftar()) { 
                frame_close()
            } 
        })
    }
    
    add_n(lblJudul)
    add(grid)
    title("Aplikasi Kasir")
    show()
}

tampilkanMenuUtama()    